group 'fr.o80.ktpathkey'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'ktpathkit'
        name = 'KtPathKit'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/olivierperez/KtPathKit.git'
        websiteUrl = 'https://github.com/olivierperez/KtPathKit'
        issueTrackerUrl = 'https://github.com/olivierperez/KtPathKit/issues'
        publicDownloadNumbers = true
        githubRepo = 'olivierperez/KtPathKit'
        githubReleaseNotesFile = 'README.md'
        version {
            name = rootProject.version
        }
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId rootProject.group
            artifactId project.name
            version rootProject.version
        }
    }
}